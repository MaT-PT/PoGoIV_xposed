apply plugin: 'com.android.application'

String gitBranch() {
    String branch = null
    try {
        Process proc = "git rev-parse --abbrev-ref HEAD".execute()
        proc.in.eachLine { line -> branch = line }
        proc.err.eachLine { line -> println line }
        proc.waitFor()
    }
    catch (ex) {}
    return branch
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.2"

    defaultConfig {
        applicationId "de.chuparch0pper.android.xposed.pogoiv"
        minSdkVersion 19
        targetSdkVersion 24
        versionCode 9
        versionName "1.5"
        String branch = gitBranch()
        project.ext.set("archivesBaseName", applicationId + "_" + versionName + (branch ? "-" + branch : ""));

        // Enabling multidex support.
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:support-v4:24.2.1'
    compile 'com.android.support:appcompat-v7:24.2.1'

    compile 'com.google.protobuf:protobuf-java:3.0.0'
    compile 'com.google.protobuf:protobuf-java-util:3.0.0'

    provided 'de.robv.android.xposed:api:53'
    provided 'de.robv.android.xposed:api:53:sources'
}

android {
    buildTypes {
        release {
            lintOptions {
                disable 'MissingTranslation'
            }
        }
    }
}